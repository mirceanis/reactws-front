buildscript {
    repositories {
        maven {
            url "http://dl.bintray.com/kunickiaj/maven"
        }
    }
}

plugins {
    id "io.errorlab.gradle.vault" version "0.1.0"
}

apply plugin: "com.android.application"

import com.android.build.OutputFile

/**
 * The react.gradle file registers a task for each build variant (e.g. bundleDebugJsAndAssets
 * and bundleReleaseJsAndAssets).
 * These basically call `react-native bundle` with the correct arguments during the Android build
 * cycle. By default, bundleDebugJsAndAssets is skipped, as in debug/dev mode we prefer to load the
 * bundle directly from the development server. Below you can see all the possible configurations
 * and their defaults. If you decide to add a configuration block, make sure to add it before the
 * `apply from: "../../node_modules/react-native/react.gradle"` line.
 *
 * project.ext.react = [
 *   // the name of the generated asset file containing your JS bundle
 *   bundleAssetName: "index.android.bundle",
 *
 *   // the entry file for bundle generation
 *   entryFile: "index.android.js",
 *
 *   // whether to bundle JS and assets in debug mode
 *   bundleInDebug: false,
 *
 *   // whether to bundle JS and assets in release mode
 *   bundleInRelease: true,
 *
 *   // whether to bundle JS and assets in another build variant (if configured).
 *   // See http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Build-Variants
 *   // The configuration property can be in the following formats
 *   //         'bundleIn${productFlavor}${buildType}'
 *   //         'bundleIn${buildType}'
 *   // bundleInFreeDebug: true,
 *   // bundleInPaidRelease: true,
 *   // bundleInBeta: true,
 *
 *   // whether to disable dev mode in custom build variants (by default only disabled in release)
 *   // for example: to disable dev mode in the staging build type (if configured)
 *   devDisabledInStaging: true,
 *   // The configuration property can be in the following formats
 *   //         'devDisabledIn${productFlavor}${buildType}'
 *   //         'devDisabledIn${buildType}'
 *
 *   // the root of your project, i.e. where "package.json" lives
 *   root: "../../",
 *
 *   // where to put the JS bundle asset in debug mode
 *   jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
 *
 *   // where to put the JS bundle asset in release mode
 *   jsBundleDirRelease: "$buildDir/intermediates/assets/release",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in debug mode
 *   resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in release mode
 *   resourcesDirRelease: "$buildDir/intermediates/res/merged/release",
 *
 *   // by default the gradle tasks are skipped if none of the JS files or assets change; this means
 *   // that we don't look at files in android/ or ios/ to determine whether the tasks are up to
 *   // date; if you have any other folders that you want to ignore for performance reasons (gradle
 *   // indexes the entire tree), add them here. Alternatively, if you have JS files in android/
 *   // for example, you might want to remove it from here.
 *   inputExcludes: ["android/**", "ios/**"],
 *
 *   // override which node gets called and with what additional arguments
 *   nodeExecutableAndArgs: ["node"],
 *
 *   // supply additional arguments to the packager
 *   extraPackagerArgs: []
 * ]
 */

apply from: "../../node_modules/react-native/react.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = true

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

apply from: "../version.gradle"
apply from: "../signing.gradle"

boolean releaseRequested = false
gradle.startParameter.taskNames.each {
    if (it.toLowerCase() ==~ /.*(release|deliver|build).*/) {
        releaseRequested = true
    }
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.reactws"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode = getBuildVersionCode()
        versionName = "0.1.${versionCode} Alpha"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        buildConfigField "boolean", "REACT_DEBUG", "false"

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk true  // If true, also generate a universal APK
            include "armeabi-v7a", "x86" //, "arm64-v8a", "x86_64", "mips"
        }
    }

    signingConfigs {
        release {
            if (releaseRequested) {
                storeFile getReleaseKeystore()
                storePassword getReleaseKeystorePassword()
                keyAlias getReleaseKeyAlias()
                keyPassword getReleaseKeyPassword()
            }
        }
        debug {
            storeFile file("../keystores/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [excludeSystemAlertWindowPermission: "false"]
            buildConfigField "boolean", "REACT_DEBUG", "true"
        }
        release {
            manifestPlaceholders = [excludeSystemAlertWindowPermission: "true"]
            buildConfigField "boolean", "REACT_DEBUG", "false"
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            if (releaseRequested) {
                signingConfig signingConfigs.release
            }
        }
        automation {
            //this is a release build as far as react-native is concerned
            // but is debuggable as an android app
            initWith(release)
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release', 'debug']
        }
    }

    testBuildType "automation"

    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a": 1, "arm64-v8a":1.1, "x86": 2, "x86_64": 2.1, "mips": 3, "mips64":3.1]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1000000 + defaultConfig.versionCode
            }
        }
    }
}

dependencies {
    implementation(project(':react-native-firebase')) {
        transitive = false
    }
    implementation project(':react-native-svg')
    implementation project(':react-native-navigation')
    implementation project(':react-native-intercom')
    implementation project(':react-native-device-info')
    implementation project(':react-native-fetch-blob')
    implementation project(':react-native-vector-icons')
    implementation project(':react-native-fcm')
    implementation project(':react-native-randombytes')
    implementation project(':react-native-keychain')
    implementation project(':react-native-image-picker')
    implementation project(':react-native-camera')
    implementation project(':react-native-intent-launcher')

    implementation "com.facebook.react:react-native:$react_native_version"
    implementation "com.onfido.sdk.capture:onfido-capture-sdk:$onfido_version"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    // From node_modules
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlytics_version@aar") {
        transitive = true
    }
    // implementation "com.instabug.library:instabug:3.2.0"
    implementation "io.intercom.android:intercom-sdk-base:$intercom_version"
    // ENABLE this once we have Firebase Messaging setup
    // implementation 'io.intercom.android:intercom-sdk-fcm:3.+'

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation "com.google.android.gms:play-services-vision:$play_services_version"
    implementation "com.google.android.gms:play-services-location:$play_services_version"
    implementation "com.google.android.gms:play-services-places:$play_services_version"
    implementation "com.google.firebase:firebase-core:$play_services_version"
    implementation "com.google.firebase:firebase-messaging:$play_services_version"
    implementation "com.google.firebase:firebase-config:$play_services_version"
    implementation "com.google.firebase:firebase-analytics:$play_services_version"

    //noinspection GradleCompatible
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"

    implementation "com.amazonaws:aws-android-sdk-core:$aws_lib_version"
    implementation "com.amazonaws:aws-android-sdk-s3:$aws_lib_version"
    implementation "com.amazonaws:aws-android-sdk-ddb:$aws_lib_version"
    implementation "com.amazonaws:aws-android-sdk-cognito:$aws_lib_version"
    implementation "com.amazonaws:aws-android-sdk-cognitoidentityprovider:$aws_lib_version"
    implementation "com.amazonaws:aws-android-sdk-sns:$aws_lib_version"

    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    androidTestImplementation "com.android.support.test:runner:$test_runner_version"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: "com.android.support", module: "support-annotations"
    })

    androidTestImplementation "com.google.code.findbugs:jsr305:3.0.0"

    testImplementation "junit:junit:$junit_version"

}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

apply plugin: 'com.google.gms.google-services'